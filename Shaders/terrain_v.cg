//PBR terrain vertex shader for PSVita

// Per frame uniforms are the same for every entity in the frame
// These don't change between draw calls, are only set once per frame

struct UniformBufferVertexMatricesPBR
{
	row_major float4x4 u_viewMatrix;
	row_major float4x4 u_projectionMatrix;
	float3 u_cameraPosition;
};
UniformBufferVertexMatricesPBR u_perVFrame : BUFFER[0];

void main(
	float3 in_position : POSITION,
	float2 in_texCoord : TEXCOORD0,
	float3 in_normal : NORMAL,
	float4 in_tangent : TANGENT, // xyz = tangent, w = precomputed handedness sign

	uniform row_major float4x4 u_modelMatrix,

	out half4 out_position : POSITION,
	out half2 pass_texCoord : TEXCOORD0_HALF,
	out half2 pass_blendMapTexCoord : TEXCOORD1_HALF,
	out half4 pass_surfaceNormal : TEXCOORD2_HALF,
	out half4 pass_worldPosition : TEXCOORD3_HALF,
	out half4 pass_surfaceToViewVector : TEXCOORD4_HALF,
	out half4 pass_tangent : TEXCOORD5_HALF // xyz = T, w = handedness
	)
{
    float4 worldPosition = mul(u_modelMatrix, float4(in_position, 1.0));

	out_position = mul(u_perVFrame.u_projectionMatrix, mul(u_perVFrame.u_viewMatrix, worldPosition));

    pass_texCoord = in_texCoord;
    pass_blendMapTexCoord = in_texCoord;

    float3 N = normalize(mul((float3x3) u_modelMatrix, in_normal));
	pass_surfaceNormal = half4(N, 0.0);

    float3 T = normalize(mul((float3x3) u_modelMatrix, in_tangent.xyz));
	pass_tangent = half4(T, in_tangent.w); // handedness precomputed in vertex data

	pass_worldPosition = half4(worldPosition);

    pass_surfaceToViewVector = half4(u_perVFrame.u_cameraPosition - worldPosition.xyz, 0.0);
}
